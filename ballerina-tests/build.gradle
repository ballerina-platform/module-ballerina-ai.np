/*
 * Copyright (c) 2025, WSO2 LLC. (http://www.wso2.org).
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 *
 */

import org.apache.tools.ant.taskdefs.condition.Os

plugins {
    id 'groovy'
}

description = 'Ballerina - Natural Programming Tests'

def packageOrg = "ballerina"
def packageName = "np"
def tomlVersion = stripBallerinaExtensionVersion("${project.version}")
def ballerinaTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/BallerinaTest.toml")
def testCommonTomlFilePlaceHolder = new File("${project.rootDir}/build-config/resources/BallerinaTestCommon.toml")
def ballerinaDist = "${project.rootDir}/target/ballerina-runtime"
def distributionBinPath =  "${ballerinaDist}/bin"
def testCoverageParam = "--code-coverage --coverage-format=xml --includes=io.ballerina.lib.np.*:ballerina.np*"
def testSuites = ["np-tests", "np-default-model-tests"]
def testCommonPackage = "np-test-commons"

def stripBallerinaExtensionVersion(String extVersion) {
    if (extVersion.matches(project.ext.timestampedVersionRegex)) {
        def splitVersion = extVersion.split('-');
        return splitVersion.length > 3 ? splitVersion[0..-4].join('-') : extVersion
    }
    return extVersion.replace("${project.ext.snapshotVersion}", "")
}

dependencies {
    testImplementation project(":np-compiler-plugin")
    testImplementation group: 'io.ballerina.openapi', name: 'ballerina-to-openapi', version: "${ballerinaToOpenApiVersion}"
}

task updateTomlFiles {
    doLast {
        testSuites.each { testPackage ->
            def ballerinaTomlFile =  new File("${project.projectDir}/${testPackage}/Ballerina.toml")
            def newBallerinaToml = ballerinaTomlFilePlaceHolder.text.replace("@project.version@", project.version)
            newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
            newBallerinaToml = newBallerinaToml.replace("@test.common@", testCommonPackage.replaceAll("-", "_"))
            newBallerinaToml = newBallerinaToml.replace("@package.name@", testPackage.replaceAll("-", "_"))
            ballerinaTomlFile.text = newBallerinaToml
        }
    }
    def ballerinaTomlFile =  new File("${project.projectDir}/${testCommonPackage}/Ballerina.toml")
    def newBallerinaToml = testCommonTomlFilePlaceHolder.text.replace("@project.version@", project.version)
    newBallerinaToml = newBallerinaToml.replace("@toml.version@", tomlVersion)
    newBallerinaToml = newBallerinaToml.replace("@package.name@", testCommonPackage.replaceAll("-", "_"))
    ballerinaTomlFile.text = newBallerinaToml
}

task commitTomlFiles {
    doLast {
        def files = "${project.projectDir}/${testCommonPackage}/Ballerina.toml ${project.projectDir}/${testCommonPackage}/Dependencies.toml "
        testSuites.each{ testPackage ->
            files += "${project.projectDir}/${testPackage}/Ballerina.toml ${project.projectDir}/${testPackage}/Dependencies.toml "
        }

        project.exec {
            ignoreExitValue true
            if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                commandLine 'cmd', '/c', "git commit -m \"[Automated] Update the native jar versions\" ${files}"
            } else {
                commandLine 'sh', '-c', "git commit -m '[Automated] Update the native jar versions' ${files}"
            }
        }
    }
}

def groupParams = ""
def disableGroups = ""
def debugParams = ""
def balJavaDebugParam = ""
def graalvmFlag = ""
def testParams = ""
def skipTests = false

task initializeVariables {
    if (project.hasProperty("groups")) {
        groupParams = "--groups ${project.findProperty('groups')}"
    }
    if (project.hasProperty("disable")) {
        disableGroups = "--disable-groups ${project.findProperty("disable")}"
    }
    if (project.hasProperty("debug")) {
        debugParams = "--debug ${project.findProperty("debug")}"
    }
    if (project.hasProperty("balJavaDebug")) {
        balJavaDebugParam = "BAL_JAVA_DEBUG=${project.findProperty("balJavaDebug")}"
    }
    if (project.hasProperty('balGraalVMTest')) {
        graalvmFlag = '--graalvm'
    }

    gradle.taskGraph.whenReady { graph ->
        if (graph.hasTask(":${packageName}-ballerina:test")) {
            if (!project.hasProperty('balGraalVMTest')) {
                testParams = "${testCoverageParam}"
            }
        } else {
            testParams = "--skip-tests"
            skipTests = true
        }
    }
}

task publishTestCommonPackageToLocal {
    dependsOn(":${packageName}-${packageOrg}:build")
    dependsOn(updateTomlFiles)
    doLast {
        if (!skipTests) {
            exec {
                workingDir "${project.projectDir}/${testCommonPackage}"
                if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                    commandLine 'cmd', '/c', "${distributionBinPath}/bal.bat pack && exit  %%ERRORLEVEL%%"
                } else {
                    commandLine 'sh', '-c', "${distributionBinPath}/bal pack"
                }
            }
            exec {
                workingDir "${project.projectDir}/${testCommonPackage}"
                if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                    commandLine 'cmd', '/c', "${distributionBinPath}/bal.bat push --repository=local" +
                            " && exit %%ERRORLEVEL%%"
                } else {
                    commandLine 'sh', '-c', "${distributionBinPath}/bal push --repository=local"
                }
            }
        }
    }
}

task ballerinaTest {
    dependsOn(publishTestCommonPackageToLocal)
    dependsOn(":${packageName}-${packageOrg}:build")
    dependsOn(updateTomlFiles)
    dependsOn(initializeVariables)
    finalizedBy(commitTomlFiles)

    doLast {
        testSuites.each { testPackage ->
            if (!skipTests) {
                exec {
                    workingDir "${project.projectDir}/${testPackage}"
                    environment "JAVA_OPTS", "-DBALLERINA_DEV_COMPILE_BALLERINA_ORG=true"
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        commandLine 'cmd', '/c', "${balJavaDebugParam} ${distributionBinPath}/bal.bat test --experimental ${graalvmFlag} ${testParams} --offline ${groupParams} ${disableGroups} ${debugParams} && exit %%ERRORLEVEL%%"
                    } else {
                        commandLine 'sh', '-c', "BALLERINA_MAX_POOL_SIZE=500 ${balJavaDebugParam} ${distributionBinPath}/bal test --experimental ${graalvmFlag} ${testParams} --offline ${groupParams} ${disableGroups} ${debugParams}"
                    }
                }
            }
        }
    }
}

test {
    dependsOn(ballerinaTest)
}

clean {
    delete "${project.projectDir}/${testCommonPackage}/target"

    testSuites.each { testPackage ->
        delete "${project.projectDir}/${testPackage}/target"
    }
}

task deleteDependencyTomlFile {
    if (project.hasProperty("deleteDependencies")) {
        delete "${project.projectDir}/${testCommonPackage}/Dependencies.toml"

        testSuites.each { testPackage ->
            delete "${project.projectDir}/${testPackage}/Dependencies.toml"
        }
    }
}

compileJava {
    doFirst {
        options.compilerArgs = [
                '--module-path', classpath.asPath,
        ]
        classpath = files()
    }
}

compileTestJava.dependsOn ":${packageName}-compiler-plugin:copyOpenApiJar"
build.dependsOn ":${packageName}-ballerina:build"
build.dependsOn ":${packageName}-compiler-plugin:build"
test.dependsOn ":${packageName}-ballerina:build"
test.dependsOn ":${packageName}-compiler-plugin:build"

publishToMavenLocal.dependsOn build
publish.dependsOn build
